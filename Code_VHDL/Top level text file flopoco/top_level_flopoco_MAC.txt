library IEEE;
use IEEE.STD_LOGIC_1164.ALL;
use IEEE.NUMERIC_STD.ALL;


entity FPMAC_Test is
    generic (
        EXP_WIDTH  : integer := 5;
        MANT_WIDTH : integer := 10
    );
    port (
        clk   : in  std_logic;
        rst   : in  std_logic;
        start : in  std_logic;
        done  : out std_logic;
        R     : out std_logic_vector(EXP_WIDTH + MANT_WIDTH downto 0)
    );
end entity;

architecture Structural of FPMAC_Test is

    ----------------------------------------------------------------
    -- COMPONENT DECLARATION: Flopoco-generated FPMAC16
    ----------------------------------------------------------------
    component FPMAC16 is
        port (
            clk       : in  std_logic;
            A         : in  std_logic_vector(EXP_WIDTH + MANT_WIDTH downto 0);
            B         : in  std_logic_vector(EXP_WIDTH + MANT_WIDTH downto 0);
            C         : in  std_logic_vector(EXP_WIDTH + MANT_WIDTH downto 0);
            negateAB  : in  std_logic;
            negateC   : in  std_logic;
            RndMode   : in  std_logic_vector(1 downto 0);
            R         : out std_logic_vector(EXP_WIDTH + MANT_WIDTH downto 0)
        );
    end component;

    ----------------------------------------------------------------
    -- CONSTANTS & TYPES
    ----------------------------------------------------------------
    constant MAC_LATENCY : integer := 8;  -- Number of cycles for FPMAC16 to produce valid output check the final report of flopoco 

    type input_array is array (0 to 15) of std_logic_vector(EXP_WIDTH + MANT_WIDTH downto 0);

    constant A_vals : input_array := (
        "1010110010111101", "0100100011011011", "0100001110000111", "0100011010111000",
        "1100010000000101", "1011100010001011", "0100010100110110", "0100100001010000",
        "0100000110001100", "1010110111000101", "1100100000110001", "0100010100001101",
        "0100010010011010", "0011010000100111", "0100001111110000", "0011110011000001"
    );

    constant B_vals : input_array := (
        "1100011010010111", "0100100010010001", "0100100001000101", "0100000101000000",
        "0011111011010101", "1100001001000001", "1100010101001010", "1100000111111100",
        "0100000101010101", "1100011100111111", "1100011011001011", "1100100010010111",
        "1100100001101110", "0100001010001000", "0100000110101010", "1011100101101010"
    );

    ----------------------------------------------------------------
    -- SIGNAL DECLARATIONS
    ----------------------------------------------------------------
    signal A, B, C_internal, R_internal : std_logic_vector(EXP_WIDTH + MANT_WIDTH downto 0) := (others => '0');
    signal negateAB  : std_logic := '0';  -- + A*B
    signal negateC   : std_logic := '0';  -- + C
    signal RndMode   : std_logic_vector(1 downto 0) := "11";  -- Round to nearest, ties to even

    signal index         : integer range 0 to 15 := 0;
    signal wait_cycles   : integer range 0 to MAC_LATENCY := 0;

    signal start_pulse   : std_logic := '0';
    signal prev_start    : std_logic := '0';
    signal fsm_done      : std_logic := '0';

    -- FSM states
    type state_type is (IDLE, WAIT_MAC, LOAD, DONE_mac);
    signal state : state_type := IDLE;

    ----------------------------------------------------------------
    -- MARK_DEBUG for ILA visibility
    ----------------------------------------------------------------
--    attribute MARK_DEBUG : string;
--    attribute MARK_DEBUG of A            : signal is "true";
--    attribute MARK_DEBUG of B            : signal is "true";
--    attribute MARK_DEBUG of C_internal   : signal is "true";
--    attribute MARK_DEBUG of R_internal   : signal is "true";
--    attribute MARK_DEBUG of index        : signal is "true";
--    attribute MARK_DEBUG of start_pulse  : signal is "true";
--    attribute MARK_DEBUG of fsm_done     : signal is "true";

begin

    ----------------------------------------------------------------
    -- Instance of the Flopoco-generated MAC block
    ----------------------------------------------------------------
    DUT : FPMAC16
        port map (
            clk       => clk,
            A         => A,
            B         => B,
            C         => C_internal,
            negateAB  => negateAB,
            negateC   => negateC,
            RndMode   => RndMode,
            R         => R_internal
        );

    ----------------------------------------------------------------
    -- Output mapping
    ----------------------------------------------------------------
    R <= C_internal;
    done <= fsm_done;

    ----------------------------------------------------------------
    -- Generate a start_pulse on rising edge of start
    ----------------------------------------------------------------
    process(clk)
    begin
        if rising_edge(clk) then
            prev_start <= start;
            start_pulse <= start and not prev_start;
        end if;
    end process;

    ----------------------------------------------------------------
    -- FSM to control the 16 A*B + C operations
    ----------------------------------------------------------------
    process(clk, rst)
    begin
        if rst = '1' then
            state        <= IDLE;
            index        <= 0;
            wait_cycles  <= 0;
            A            <= (others => '0');
            B            <= (others => '0');
            C_internal   <= (others => '0');
            fsm_done     <= '0';

        elsif rising_edge(clk) then
            case state is
                when IDLE =>
                    fsm_done <= '0';
                    wait_cycles <= 0;
                    if start_pulse = '1' then
                        index <= 0;
                        A <= A_vals(0);
                        B <= B_vals(0);
                        C_internal <= (others => '0');
                        wait_cycles <= 1;
                        state <= WAIT_MAC;
                    end if;

                when WAIT_MAC =>
                    -- Wait for MAC_LATENCY cycles to get valid R_internal
                    if wait_cycles < MAC_LATENCY then
                        wait_cycles <= wait_cycles + 1;
                    else
                        state <= LOAD;
                    end if;

                when LOAD =>
                    -- Use R_internal as the next C
                    C_internal <= R_internal;
                    if index < 15 then
                        index <= index + 1;
                        A <= A_vals(index + 1);
                        B <= B_vals(index + 1);
                        wait_cycles <= 1;
                        state <= WAIT_MAC;
                    else
                        fsm_done <= '1';
                        state <= DONE_mac;
                    end if;

                when DONE_mac =>
                    -- Remain here until reset
                    state <= DONE_mac;

                when others =>
                    state <= IDLE;
            end case;
        end if;
    end process;

end Structural;

